buildscript {
  repositories {
    jcenter()

    // Needed for ml-unit-test-client dependency until it's available via jcenter()
    maven {
      url {"https://dl.bintray.com/rjrudin/maven/"}
    }
  }
  dependencies {
    classpath "com.marklogic:ml-unit-test-client:0.11.1"
    classpath "com.marklogic:ml-gradle:${mlGradleVersion}"
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "com.jfrog.bintray" version "1.7.3"
  id "com.marklogic.ml-gradle" version "3.6.3"
  id 'net.saliman.properties' version '1.4.6'
}

repositories {
  jcenter()

  // Needed for ml-unit-test and ml-unit-test-client dependencies until they're available via jcenter()
  maven {
    url {"https://dl.bintray.com/rjrudin/maven/"}
  }
}

dependencies {
  mlRestApi "com.marklogic:ml-unit-test-modules:0.11.1"

  // For running ml-unit-test tests via JUnit
  testCompile "com.marklogic:ml-unit-test-client:0.11.1"
  testCompile "junit:junit:4+"
}

bintray {
  user = bintray_user
  key = bintray_key
  publications = ['mainModules', 'sourcesModules']
  pkg {
    repo = 'Maven'
    name = "smart-mastering-core"
    licenses = ["Apache-2.0"]
    vcsUrl = "https://github.com/marklogic-community/smart-mastering-core.git"
    version {
      name = project.version
      released = new Date()
    }
  }
}

// Defines a configuration for the MarkLogic modules; used by the modulesZip task below
configurations {
  modules
  sources
}

task modulesJar(type: Jar) {
  description = "Jar up the smart-mastering-core MarkLogic modules into a package that can be published"
  from("src/main/ml-modules") {
    into("smart-mastering-core/ml-modules")
  }
  baseName "smart-mastering-core"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  description = "A sources jar is needed for publishing to jcenter; it has the same contents as the modulesJar"
  from("src/main/ml-modules")
  baseName "smart-mastering-core"
  classifier "sources"
}

artifacts {
  modules modulesJar
  sources sourcesJar
}

publishing {
  publications {
    mainModules(MavenPublication) {
      artifactId "smart-mastering-core"
      artifact modulesJar
    }
    sourcesModules(MavenPublication) {
      artifactId "smart-mastering-core"
      artifact sourcesJar
    }
  }
  repositories {
    mavenCentral()
  }
}

